apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		jcenter()
	}
}

sourceCompatibility = 1.8
version = '0.2.1'

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version = version + "." + "${System.getenv().BUILD_NUMBER}"
}

group = 'net.fabricmc'
archivesBaseName = 'tiny-remapper'

repositories {
	mavenCentral()
}

dependencies {
	compile 'org.ow2.asm:asm:7.0'
	compile 'org.ow2.asm:asm-commons:7.0'
	compile 'org.ow2.asm:asm-tree:7.0'
	compile 'org.ow2.asm:asm-util:7.0'
}

apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/maven.gradle'
apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/license.gradle'

jar {
	manifest {
		attributes	'Implementation-Title': 'TinyRemapper',
					'Implementation-Version': version,
					'Main-Class': "net.fabricmc.tinyremapper.Main"
	}
}

task fatJar(type: Jar) {
    from {
		configurations.compile.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
	manifest {
		attributes	'Implementation-Title': 'TinyRemapper',
					'Implementation-Version': version,
					'Main-Class': "net.fabricmc.tinyremapper.Main"
	}
	classifier = 'fat'
	with jar
}

task sourceJar(type:Jar) {
	classifier = "sources"
	from sourceSets.main.allSource
}

artifacts {
   archives fatJar
   archives sourceJar
}
